/*
 * Copyright (C) 2015-2016 SÃ©bastiaan (github.com/se-bastiaan)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

version = VERSION_NAME
group = GROUP

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError true
    }
}

ext {
    libtorrentVersion = '1.2.0.16'
}

dependencies {
    api "com.frostwire:jlibtorrent:$libtorrentVersion"
    implementation "com.frostwire:jlibtorrent-android-arm:$libtorrentVersion"
    implementation "com.frostwire:jlibtorrent-android-arm64:$libtorrentVersion"
    implementation "com.frostwire:jlibtorrent-android-x86:$libtorrentVersion"
    implementation "com.frostwire:jlibtorrent-android-x86_64:$libtorrentVersion"
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key =  System.getenv('BINTRAY_KEY')

    publish = true

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = ARTIFACT
        desc = DESCRIPTION
        userOrg = ORGANIZATION
        websiteUrl = REPO_URL
        issueTrackerUrl = URL_ISSUES
        vcsUrl = SCM_URL
        licenses = ['GPL-3.0']
        labels = ['android', 'torrent', 'streamer']
        publicDownloadNumbers = true

        githubRepo = 'butterproject/TorrentStream-Android'
        githubReleaseNotesFile = 'README.md'
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging PACKAGING
                artifactId ARTIFACT
                name PROJECT_NAME
                url REPO_URL
                licenses {
                    license {
                        name LICENSE_NAME
                        url LICENSE_URL
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id "blazsolar"
                        name "Blaz Solar"
                    }
                }
                scm {
                    url REPO_URL
                    connection SCM_URL
                    developerConnection SCM_URL
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options {
        encoding = 'UTF-8'
        failOnError false
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
